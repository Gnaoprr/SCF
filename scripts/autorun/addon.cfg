installed_addons = ""

initialize_custom = [
	echo (concatword $colorintro "[SCF]: ^f7Initializing ^f1custom ^f7^"" $arg1 "^"...")
	exec (concatword "scripts/addons/" $current_addon_folder "/" $arg1)
	echo (concatword $colorintro "[SCF]: ^f2OK ^f7[^f1custom ^f7^"" $arg1 "^"].")
]

addaddoncommand = [
	addcommand $arg1 (concatword "addons/" $current_addon_folder "/" $arg2) $arg3 $arg4
]

loopfiles folder "scripts/addons/" "" [
	if (&& (!=s $folder ".") (!=s $folder "..")) [
		has_addon_init = 0
		loopfiles file (concatword "scripts/addons/" $folder "/autorun/") "cfg" [
			if (=s $file "init") [
				has_addon_init = 1
			]
		]
		if (= $has_addon_init 1) [
			installed_addons = (concat $installed_addons @folder)
			echo (concatword $colorintro "[SCF]: ^f7Initializing ^f1addon ^f7^"" @folder "^"...")
			current_addon_folder = @folder
			exec (concatword "scripts/addons/" @folder "/autorun/init.cfg")
			echo (concatword $colorintro "[SCF]: ^f2OK ^f7[^f1addon ^f7^"" @folder "^"].")
		] [
			installed_addons = (concat $installed_addons @folder)
			echo (concatword $colorintro "[SCF]: ^f7Initializing ^f1addon ^f7^"" @folder "^"...")
			current_addon_folder = @folder
			loopfiles file (concatword "scripts/addons/" $current_addon_folder "/autorun/") "cfg" [
				exec (concatword "scripts/addons/" $current_addon_folder "/autorun/" $file ".cfg")
			]
			echo (concatword $colorintro "[SCF]: ^f2OK ^f7[^f1addon ^f7^"" @folder "^"].")
		]
	]
]

downloadexec = [
	downloadfile $arg1 $arg2
	exec $arg2
]